elements: WS* element+
?element: field content
?field: NEWLINE* "#[" FIELD_NAME "]#" NEWLINE
content: contents*
?contents: paragraph
    | code_block
code_block: CODE_BLOCK_START (LINE? NEWLINE)+
paragraph: PARAGRAPH_START? (paragraph_line | bullets | enumeration | footnote)+
bullets: bullet+
bullet: BULLET paragraph_line
enumeration: item+
item: ENUMERATION_ITEM_START paragraph_line
footnote: FOOTNOTE_START LINE NEWLINE+
paragraph_line: (PARAGRAPH_LINE | FOOTNOTE_ANCHOR | monospace_text | italics_text | bold_text)* NEWLINE
italics_text: UNDERSCORE_START UNDERSCORE_TEXT UNDERSCORE_END
bold_text: STAR_START STAR_TEXT STAR_END
monospace_text: "`" BACKTICK_TEXT "`"

NEWLINE: /[\r\n]/
WS: /\s/
FIELD_NAME: /[^\]]+/
FOOTNOTE_START: /bc\.\.? fn\d\. /
CODE_BLOCK_START: /bc\.\.? /
PARAGRAPH_START: /p\. /
LINE.-1: /.+/

// Priority lower than LINE, but higher than PARAGRAPH_LINE, to only match in paragraphs
BULLET.-2: /[\*#] +/
STAR_START.-2: /(?<=[\s\.,])\*/
UNDERSCORE_START.-2: /(?<=[\s\.,])_/
STAR_END.-2: /\*(?=[\s\.,])/
UNDERSCORE_END.-2: /_(?=[\s\.,])/
ENUMERATION_ITEM_START.-2: /\d+\. +/
FOOTNOTE_ANCHOR.-2: /<\d>/
BACKTICK_TEXT.-2: /[^`]+/
STAR_TEXT.-2: /[^*]+/
UNDERSCORE_TEXT.-2: /[^_]+/

// Stop at FOOTNOTE_ANCHOR
PARAGRAPH_LINE.-3: /.+?(?=(<\d>|\n|\r|\*|`|_))/
